"""
snakemake -p -n -j 10 -d /mnt/data/project0061 -C \
        sample_id='Mira_1' \
        bam='dario/Mira_1.sample.bam' \
        gff='ref/schistosoma_mansoni.PRJEA36577.WBPS19.annotations.gff3' \
        genome=ref/schistosoma_mansoni.PRJEA36577.WBPS19.genomic_softmasked.fa \
        read_mode=single \
        fragment_length=300 \
        fragment_sd=100

"""

sample_id = config["sample_id"]

os.makedirs("slurm", exist_ok=True)


def get_fastq(wildcards):
    if config["read_mode"] == "single":
        return f"fastq/{wildcards.sample_id}_0.fq.gz"
    elif config["read_mode"] == "paired":
        return [
            f"fastq/{wildcards.sample_id}_1.fq.gz",
            f"fastq/{wildcards.sample_id}_2.fq.gz",
        ]
    else:
        sys.exit("Invalid read mode")


rule all:
    input:
        expand('wp3_add_ic_tag/{sample_id}.bam', sample_id=sample_id),


rule stringtie:
    input:
        bam=config["bam"],
        gff=config["gff"],
    output:
        gtf="stringtie/{sample_id}.gtf",
    shell:
        r"""
        stringtie -o {output.gtf} \
            -p 24 \
            -G {input.gff} {input.bam} 
        """


rule fastaForTranscripts:
    input:
        gtf="stringtie/{sample_id}.gtf",
        genome=config["genome"],
    output:
        fa="stringtie/{sample_id}.fa",
    shell:
        r"""
        gffread -w {output.fa} \
            -g {input.genome} \
            {input.gtf}
        """


rule kallisto_index:
    input:
        fa="stringtie/{sample_id}.fa",
    output:
        idx="kallisto/{sample_id}.idx",
    conda:
        "env/kallisto.yml"
    shell:
        r"""
        kallisto index -i {output.idx} {input.fa}
        """


rule get_fastq_from_bam:
    input:
        bam=config["bam"],
    output:
        fq0="fastq/{sample_id}_0.fq.gz",
        fq1="fastq/{sample_id}_1.fq.gz",
        fq2="fastq/{sample_id}_2.fq.gz",
    shell:
        r"""
        samtools collate -@ 8 -u --no-PG {input.bam} -O \
        | samtools fastq -@ 8 -0 {output.fq0} -1 {output.fq1} -2 {output.fq2}
        """


rule kallisto_quant:
    input:
        idx="kallisto/{sample_id}.idx",
        fq=["fastq/{sample_id}_0.fq.gz"],
    output:
        "kallisto/{sample_id}/pseudoalignments.bam",
    conda:
        "env/kallisto.yml"
    params:
        fq=get_fastq,
        single="--single" if config["read_mode"] == "single" else "",
        fragment_length=config["fragment_length"],
        fragment_sd=config["fragment_sd"],
    shell:
        r"""
        kallisto quant {params.single} -l {params.fragment_length} -s {params.fragment_sd} \
            --threads 24 --pseudobam -o kallisto/{wildcards.sample_id} -i {input.idx} {params.fq}
        """


rule read_to_transcripts_tsv:
    input:
        bam="kallisto/{sample_id}/pseudoalignments.bam",
        gtf="stringtie/{sample_id}.gtf",
    output:
        tsv="kallisto/{sample_id}/read_transcripts.tsv.gz",
    shell:
        r"""
        {workflow.basedir}/scripts/read_to_transcripts_tsv.py \
            --bam {input.bam} \
            --gtf {input.gtf} \
        | gzip > {output.tsv}
        """


rule tx_name_to_id:
    input:
        tsv="kallisto/{sample_id}/read_transcripts.tsv.gz",
    output:
        tsv="kallisto/{sample_id}/clusters.tsv",
    shell:
        r"""
cat <<'EOF' > {rule}.$$.tmp.R
library(data.table)

tx <- fread('{input.tsv}')
tx[, cluster_id := as.numeric(as.factor(transcript_id))]
fwrite(x=tx[, list(`#read_id`, cluster_id)], file='{output.tsv}', col.names=FALSE, sep='\t')
EOF
Rscript {rule}.$$.tmp.R
rm {rule}.$$.tmp.R
       """

rule add_ic_tag:
    input:
        tsv="kallisto/{sample_id}/clusters.tsv",
        bam=config['bam']
    output:
        bam='wp3_add_ic_tag/{sample_id}.bam',
    shell:
        r"""
        {workflow.basedir}/../src/python/bam_manipulation/wp3_add_ic_tag.py \
            -i {input.bam} \
            -c {input.tsv} \
            -o {output.bam}
        """

# rule kallisto_sort:
#     input:
#         bam='kallisto/{sample_id}/pseudoalignments.bam',
#     output:
#         bam='kallisto/{sample_id}/pseudoalignments.sort.bam',
#     shell:
#         r"""
#         samtools sort -@ 8 {input.bam} > {output.bam}
#         """
#
#
# rule index_bam:
#     input:
#         bam='kallisto/{sample_id}/pseudoalignments.sort.bam',
#     output:
#         bam='kallisto/{sample_id}/pseudoalignments.sort.bam.bai',
#     shell:
#         r"""
#         samtools index -@ 8 {input.bam}
#         """
#
#
# rule bwa_index:
#     input:
#         fa='stringtie/{sample_id}.fa',
#     output:
#         idx='stringtie/{sample_id}.fa.bwt',
#
# rule bwa_mem:
#     input:
#         fa='stringtie/{sample_id}.fa',
#         idx='stringtie/{sample_id}.fa.bwt',
#         fq=['data/fastq/ERR11178264_1.fastq.gz', 'data/fastq/ERR11178264_2.fastq.gz'],
#     output:
#         bam='bwa/{sample_id}.bam',
#     shell:
#         r"""
#         bwa mem -t 24 {input.fa} {input.fq} \
#         | samtools view -b > {output.bam}
#         """
#
#
# rule download:
#    output:
#        "ref/schistosoma_mansoni.PRJEA36577.WBPS19.genomic_softmasked.fa",
#        "schistosoma_mansoni.PRJEA36577.WBPS19.annotations.gff3",
#    shell:
#        r"""
#        cd ref
#        wget https://ftp.ebi.ac.uk/pub/databases/wormbase/parasite/releases/WBPS19/species/schistosoma_mansoni/PRJEA36577/schistosoma_mansoni.PRJEA36577.WBPS19.genomic_softmasked.fa.gz
#        wget https://ftp.ebi.ac.uk/pub/databases/wormbase/parasite/releases/WBPS19/species/schistosoma_mansoni/PRJEA36577/schistosoma_mansoni.PRJEA36577.WBPS19.annotations.gff3.gz
#        gunzip schistosoma_mansoni.PRJEA36577.WBPS19.genomic_softmasked.fa.gz
#        gunzip schistosoma_mansoni.PRJEA36577.WBPS19.annotations.gff3.gz
#        samtools faidx ref/schistosoma_mansoni.PRJEA36577.WBPS19.genomic_softmasked.fa
#        """
